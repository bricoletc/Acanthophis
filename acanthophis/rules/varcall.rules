# These rules are part of Acanthophis. See https://github.com/kdm9/Acanthophis.
# This file *could* be modified, but then be careful when you update them. And
# please, if you find a bug, raise an issue on github so the fix gets shared
# with everyone.
#
# Copyright 2016-2022 Kevin Murray/Gekkonid Consulting
#
# This Source Code Form is subject to the terms of the Mozilla Public License,
# v. 2.0. If a copy of the MPL was not distributed with this file, You can
# obtain one at http://mozilla.org/MPL/2.0/.

#######################################################################
#                               Mpileup                               #
#######################################################################
#
# For mpileup, we don't merge the bams but make a list of bam files and use that. 

rule mpileup_bamlist:
    input:
        lambda wc: P(expand("alignments/samples/{aligner}/{ref}/{sample}.bam",
                            aligner=wc.aligner, ref=wc.ref, sample=config["SAMPLESETS"][wc.sampleset])),
    output:
        temp(T("variants/raw_split/mpileup~{aligner}~{ref}~{sampleset}-{region}.bamlist")),
    group: "varcall_oneregion"
    run:
        with open(output[0], "w") as fh:
            for s in input.bam:
                print(s, file=fh)

rule mpileup:
    input:
        bam=P("alignments/sets/{aligner}~{ref}~{sampleset}.bam"),
        bai=P("alignments/sets/{aligner}~{ref}~{sampleset}.bam.bai"),
        ref=lambda wc: R(config["data_paths"]["references"][wc.ref]["fasta"], keep_local=True),
    output:
        bcf=P("variants/raw_split/mpileup~{aligner}~{ref}~{sampleset}/{region}.bcf"),
    log: L("variants/raw_split/mpileup~{aligner}~{ref}~{sampleset}/{region}.bcf.log"),
    params:
        theta=lambda wc: config["tool_settings"]["varcall"]["theta_prior"].get(wc.sampleset, 0.01),
        minmq=lambda wc: config["tool_settings"]["varcall"]["minmapq"].get(wc.aligner, 5),
        max_depth=lambda wc: config["tool_settings"]["varcall"]["max_depth"].get("mpileup", 1000),
        minbq=config["tool_settings"]["varcall"]["minbq"],
    group: "varcall_oneregion"
    resources: **rule_resources(config, "mpileup", time_min=120, mem_gb=4, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "( bcftools mpileup"
        "   --redo-BAQ"
        "   --max-depth {params.max_depth}" # per file (i.e. sample) max depth
        "   --min-MQ {params.minmq}"
        "   --min-BQ {params.minbq}"
        "   --fasta-ref {input.ref}"
        "   --annotate FORMAT/DP,FORMAT/AD,FORMAT/SP,INFO/AD" #output extra tags
        "   --region '{wildcards.region}'"
        "   --output-type u" # uncompressed bam
        "   {input.bam}"
        " | bcftools call"
        "   --threads {threads}"
        "   --targets '{wildcards.region}'" # might not be needed
        "   --multiallelic-caller"
        "   --prior {params.theta}"
        "   -O b" # compressed bam
        "   -o {output.bcf}"
        " ) >{log} 2>&1"



#######################################################################
#                              Freebayes                              #
#######################################################################
rule freebayes:
    input:
        bam=P("alignments/sets/{aligner}~{ref}~{sampleset}.bam"),
        bai=P("alignments/sets/{aligner}~{ref}~{sampleset}.bam.bai"),
        ref=lambda wc: R(config["data_paths"]["references"][wc.ref]["fasta"], keep_local=True),
    output:
        bcf=P("variants/raw_split/freebayes~{aligner}~{ref}~{sampleset}/{region}.bcf"),
    log: L("variants/raw_split/freebayes~{aligner}~{ref}~{sampleset}/{region}.bcf.log"),
    group: "varcall_oneregion"
    params:
        theta=lambda wc: config["tool_settings"]["varcall"]["theta_prior"].get(wc.sampleset, 0.01),
        minmq=lambda wc: config["tool_settings"]["varcall"]["minmapq"].get(wc.aligner, 5),
        max_depth=lambda wc: config["tool_settings"]["varcall"]["max_depth"].get("freebayes", 1000),
        minbq=config["tool_settings"]["varcall"]["minbq"],
    resources: **rule_resources(config, "freebayes", time_min=240, mem_gb=8, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "( freebayes"
        "   --theta {params.theta}"
        "   --ploidy 2"
        "   --use-best-n-alleles 4"
        "   --min-mapping-quality {params.minmq}"
        "   --min-base-quality {params.minbq}"
        "   --read-max-mismatch-fraction 0.10"
        "   --skip-coverage {params.max_depth}"
        "   --prob-contamination 1e-3"
        "   --strict-vcf"
        "   --region '{wildcards.region}'"
        "   --fasta-reference {input.ref}"
        "   {input.bam}"
        " | bcftools view"
        "   -O b"
        "   -o {output.bcf}"
        " ) >{log} 2>&1"


#######################################################################
#                            Bcftools Norm                            #
#######################################################################
rule bcfnorm:
    input:
        bcf=P("variants/raw_split/{caller}~{aligner}~{ref}~{sampleset}/{region}.bcf"),
        ref=lambda wc: R(config["data_paths"]["references"][wc.ref]["fasta"]),
    output:
        # Not a pipe! can't run multiple filters if a pipe
        bcf=temp(T("variants/norm_split/{caller}~{aligner}~{ref}~{sampleset}/{region}.bcf")),
    log: L("variants/norm_split/{caller}~{aligner}~{ref}~{sampleset}/{region}.bcf.log"),
    group: "varcall_oneregion"
    resources: **rule_resources(config, "bcfnorm", time_min=120, mem_gb=4, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "( bcftools norm"
        "   --fasta-ref {input.ref}"
        "   --multiallelics -snps"  # Split multi-alleics to filter each allele separately
        "   -O u"
        "   -o {output.bcf}"
        "   {input.bcf}"
        " ) >{log} 2>&1"


#######################################################################
#                           Bcftools filter                           #
#######################################################################
rule bcffilter:
    input:
        bcf=T("variants/norm_split/{caller}~{aligner}~{ref}~{sampleset}/{region}.bcf"),
        ref=lambda wc: R(config["data_paths"]["references"][wc.ref]["fasta"], keep_local=True),
    output:
        # Not a pipe! can't run all regions separately if this is a pipe into merge
        bcf=temp(T("variants/filter_split/{caller}~{aligner}~{ref}~{sampleset}_filtered~{filter}/{region}.bcf")),
    log:
        L("variants/filter_split/{caller}~{aligner}~{ref}~{sampleset}_filtered~{filter}/{region}.bcf.log"),
    params:
        filtarg=lambda wc: config["tool_settings"]["varcall"]["filters"][wc.filter].replace('\n', ' ')
    group: "varcall_oneregion"
    resources: **rule_resources(config, "bcffilter", time_min=120, mem_gb=4, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "( bcftools view"
        "   {params.filtarg}"
        "   -O u"
        "   {input.bcf}"
        " | bcftools norm" # We normalise here to re-join multi-allelic sites, after filtering with multi-allelics split
        "   --fasta-ref {input.ref}"
        "   --do-not-normalize"
        "   --multiallelics +snps" # re-join multi-alleic sites
        "   -O u"
        "   -o {output.bcf}"
        " ) >{log} 2>&1"




#######################################################################
#                              Bcf Merge                              #
#######################################################################
rule bcfmerge_fofn:
    input:
        bcf=lambda wc: expand(T("variants/filter_split/{caller}~{aligner}~{ref}~{sampleset}_filtered~{filter}/{region}.bcf"),
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset, filter=wc.filter,
                              region=sorted(config["VARCALL_REGIONS"][wc.caller][wc.ref])),
    output:
        fofn=temp(T("variants/final/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf.INPUT_FOFN")),
    group: "bcfmerge"
    run:
        with open(output[0], "w") as fh:
            for s in sorted(input):
                print(s, file=fh)


rule premergevariantidx:
    input:
        T("variants/filter_split/{path}")
    output:
        T("variants/filter_split/{path}.csi")
    log:
        T("variants/filter_split/{path}.csi.log")
    resources: **rule_resources(config, "variantidx", time_min=720, mem_gb=8, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "bcftools index -f {input}"


rule bcfmerge:
    input:
        bcf=lambda wc: T(expand("variants/filter_split/{caller}~{aligner}~{ref}~{sampleset}_filtered~{filter}/{region}.bcf",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset, filter=wc.filter,
                              region=sorted(config["VARCALL_REGIONS"][wc.caller][wc.ref]))),
        bcfi=lambda wc: T(expand("variants/filter_split/{caller}~{aligner}~{ref}~{sampleset}_filtered~{filter}/{region}.bcf.csi",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset, filter=wc.filter,
                              region=sorted(config["VARCALL_REGIONS"][wc.caller][wc.ref]))),
        fofn=T("variants/final/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf.INPUT_FOFN"),
    output:
        bcf=P("variants/final/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf"),
    log:
        L("variants/final/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf.log"),
    params:
        allow_overlaps=lambda wc: "--allow-overlaps" if config["tool_settings"]["varcall"]["merge_allow_overlaps"].get(wc.caller, False) else ""
    group: "bcfmerge"
    resources: **rule_resources(config, "bcfstats", time_min=1440, mem_gb=12, cores=64)
    conda: "envs/varcall.yml"
    shell:
        "( bcftools concat"
        "   --threads {threads}"
        "   --file-list {input.fofn}"
        "   {params.allow_overlaps}"
        "   -o {output.bcf}"
        "   -O b"
        " ) >{log} 2>&1"


#######################################################################
#                              BCF stats                              #
#######################################################################
rule bcfstats:
    input:
        P("variants/{path}.bcf")
    output:
        P("variants/{path}.bcf.stats")
    log: L("variants/{path}.bcf.stats.log")
    conda: "envs/varcall.yml"
    resources: **rule_resources(config, "bcfstats", time_min=120, mem_gb=4, cores=4)
    shell:
        "bcftools stats -s - -d 0,1000,1 --threads {threads} {input} >{output}"




#######################################################################
#                       Misc variant processing                       #
#######################################################################
rule bcf2vcf:
    input:
        bcf=P("{path}.bcf"),
    output:
        vcf=P("{path}.vcf.gz"),
    log: L("{path}.vcf.gz.log"),
    resources: **rule_resources(config, "bcf2vcf", time_min=720, mem_gb=8, cores=8)
    conda: "envs/varcall.yml"
    shell:
        "( bcftools view"
        "   {input.bcf}"
        "   -O z"
        "   --threads {threads}"
        "   -o {output.vcf}"
        " ) >{log} 2>&1"

rule finalvariantidx:
    input:
        P("variants/final/{path}")
    output:
        P("variants/final/{path}.csi")
    resources: **rule_resources(config, "variantidx", time_min=720, mem_gb=8, cores=1)
    conda: "envs/varcall.yml"
    shell:
        "bcftools index -f {input}"


#######################################################################
#                             Target Rules                            #
#######################################################################
rule all_filtered_variants:
    input:
        [P(expand("variants/final/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.{ext}",
               ext=["bcf", "bcf.csi", "vcf.gz", "vcf.gz.csi", "bcf.stats"],
               caller=config["samplesets"][sampleset]["varcall"]["callers"],
               aligner=config["samplesets"][sampleset]["varcall"]["aligners"],
               ref=config["samplesets"][sampleset]["varcall"]["refs"],
               filter=config["samplesets"][sampleset]["varcall"]["filters"],
               sampleset=sampleset))
         for sampleset in config["samplesets"]
         if "varcall" in config["samplesets"][sampleset]
         ],


rule all_varcall:
    input:
        rules.all_filtered_variants.input,
